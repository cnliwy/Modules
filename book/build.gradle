// 为true时表示此Module是一个独立的应用，为false时表示此Module是一个library
if (isDebug.toBoolean()){
    apply plugin: 'com.android.application'
}else{
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        // 作为library时不能有applicationId,只有作为一个独立应用时才能够如下设置
        if (isDebug.toBoolean()){
            applicationId "com.liwy.book"
        }
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        // 不同形态加载不同的AndroidManifest.xml文件，切换至Project目录状态下，在src/main/下新建两个目录：debug和release，并分别拷入AndroidManifest.xml
        main {
            if (isDebug.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
                java.srcDirs += "src/main/debug/java/";
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
                java.srcDirs += "src/main/release/java/";
            }
        }
    }
    // 为防止资源冲突，设置资源别名，music下所有的资源名称都要以“music_”开头，否则会报错的哦！
    resourcePrefix "book_"
}

dependencies {
    // 加载公用library
    compile project(':library')
}
